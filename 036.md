Original Powder Kookaburra

medium

# `degenSubAccount` uses wrong formula for `getMaxWithdrawAmount`

## Summary
`degenSubAccount` uses wrong formula for `getMaxWithdrawAmount`

## Vulnerability Detail
Let's look at the `getMaxWithdrawAmount` formula in `degenSubAccount` 
```solidity
    function getMaxWithdrawAmount(address trader) public view returns (uint256, uint256) {
        (int256 primaryCredit,, uint256 pendingPrimaryWithdraw,,) = IDealer(dealer).getCreditOf(address(this));

        uint256 positionMargin;
        int256 positionNetValue;
        address[] memory positions = IDealer(dealer).getPositions(trader);

        for (uint256 i = 0; i < positions.length;) {
            (int256 paperAmount, int256 creditAmount) = IPerpetual(positions[i]).balanceOf(trader);

            Types.RiskParams memory params = IDealer(dealer).getRiskParams(positions[i]);
            int256 price = SafeCast.toInt256(IPriceSource(params.markPriceSource).getMarkPrice());
            positionMargin += (paperAmount.decimalMul(price).abs() * 1e16) / 1e18;

            positionNetValue += paperAmount.decimalMul(price) + creditAmount;
            unchecked {
                ++i;
            }
        }
```
When we look at it, it's different from the function used in `Liquidation.sol` (looking at initialMargin)
```solidity
    function getTotalExposure(
        Types.State storage state,
        address trader
    )
        public
        view
        returns (int256 netValue, uint256 exposure, uint256 initialMargin, uint256 maintenanceMargin)
    {
        int256 netPositionValue;
        // sum net value and exposure among all markets
        for (uint256 i = 0; i < state.openPositions[trader].length;) {
            (int256 paperAmount, int256 creditAmount) = IPerpetual(state.openPositions[trader][i]).balanceOf(trader);
            Types.RiskParams storage params = state.perpRiskParams[state.openPositions[trader][i]];
            int256 price = SafeCast.toInt256(IPriceSource(params.markPriceSource).getMarkPrice());

            netPositionValue += paperAmount.decimalMul(price) + creditAmount;
            uint256 exposureIncrement = paperAmount.decimalMul(price).abs();
            exposure += exposureIncrement;
            maintenanceMargin += (exposureIncrement * params.liquidationThreshold) / Types.ONE;
            initialMargin += (exposureIncrement * params.initialMarginRatio) / Types.ONE;
            unchecked {
                ++i;
            }
        }
        netValue = netPositionValue + state.primaryCredit[trader] + SafeCast.toInt256(state.secondaryCredit[trader]);
    }
```

This would cause the value in `getMaxWithdrawAmount` to be completely off. Anything that depends on it will not work. 
It will also cause the `require` in `requestWithdrawPrimaryAsset` `executeWithdrawPrimaryAsset` and `fastWithdrawPrimaryAsset` to not work properly.

## Impact
Wrong values returned, anything that depends on it will not work as well, including the require checks within the contract. 

## Code Snippet
https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/main/smart-contract-EVM/src/subaccount/DegenSubaccount.sol#L59C1-L84C6

## Tool used

Manual Review

## Recommendation
use the formula from `Liquidation.sol` 


### PoC 
```solidity
    function testDegenSubaccountOpenPosition() public {
        vm.startPrank(traders[0]);
        jojoDealer.deposit(0, 1000e6, traders[0]);
        vm.stopPrank();

        vm.startPrank(traders[1]);
        jojoDealer.deposit(0, 10_000e6, traders[1]);
        vm.stopPrank();

        vm.startPrank(traders[2]);
        jojoDealer.deposit(0, 1000e6, traders[2]);
        vm.stopPrank();

        DegenSubaccountFactory degenFac = new DegenSubaccountFactory(address(jojoDealer), address(this));
        (Types.Order memory basedorder, bytes memory basedSig) =
            buildOrder(traders[0], tradersKey[0], 1e18, -30_000e6, address(perpList[0]), 0);
        vm.startPrank(traders[1]);
        usdc.approve(address(jojoDealer), 1000e6);
        address degenAccount = degenFac.newSubaccount();
        jojoDealer.deposit(1000e6, 0, degenAccount);
        (Types.Order memory order, bytes memory orderSig) =
            buildOrder(degenAccount, tradersKey[1], -1e18, 30_000e6, address(perpList[0]), 0);
        Types.Order[] memory orderList = new Types.Order[](2);
        orderList[0] = basedorder;
        orderList[1] = order;
        bytes[] memory signatureList = new bytes[](2);
        signatureList[0] = basedSig;
        signatureList[1] = orderSig;
        uint256[] memory matchPaperAmount = new uint256[](2);
        matchPaperAmount[0] = 1e18;
        matchPaperAmount[1] = 1e18;
        vm.stopPrank();
        Perpetual(perpList[0]).trade(abi.encode(orderList, signatureList, matchPaperAmount));
        // revert for netValue less than 0
        (uint256 maxWithdraw, uint256 pendingPrimary) = DegenSubaccount(degenAccount).getMaxWithdrawAmount(degenAccount);
        console.log(maxWithdraw);
        vm.startPrank(traders[1]);
        DegenSubaccount(degenAccount).requestWithdrawPrimaryAsset(maxWithdraw);
        cheats.expectRevert("JOJO_ACCOUNT_NOT_SAFE");
        DegenSubaccount(degenAccount).executeWithdrawPrimaryAsset(traders[1], true);
}
```
