Glamorous Metal Albatross

medium

# After withdraw user can be subject to immediate liquidation

## Summary
Issue arises from different formulas that are used in health check after withdraw and in liquidation.

## Vulnerability Detail
Here you can see that `accrueRate()` compounds interest on every call, while `getTRate()` calculates plain interest:
```solidity
    function accrueRate() public {
        uint256 currentTimestamp = block.timestamp;
        if (currentTimestamp == lastUpdateTimestamp) {
            return;
        }
        uint256 timeDifference = block.timestamp - uint256(lastUpdateTimestamp);
@>      tRate = tRate.decimalMul((timeDifference * borrowFeeRate) / Types.SECONDS_PER_YEAR + 1e18);
        lastUpdateTimestamp = currentTimestamp;
    }

    function getTRate() public view returns (uint256) {
        uint256 timeDifference = block.timestamp - uint256(lastUpdateTimestamp);
@>      return tRate + (borrowFeeRate * timeDifference) / Types.SECONDS_PER_YEAR;
    }
```

To show you they return different values, suppose tRate = 2, timeDifference = 1 year, borrowFeeRate = 10%. `accrueRate()` will calculate `2 * (1 * 10% / 1 + 1) = 2.2`.
At the same time `getTRate()` will calculate `2 + (1 * 10% / 1) = 2.1`.

## Impact
User can be immediately liquidated after collateral withdrawal.
In withdraw it uses `getTRate()` to check health:
```solidity
    function withdraw(
        address collateral,
        uint256 amount,
        address to,
        bool isInternal
    )
        external
        override
        nonReentrant
        nonFlashLoanReentrant
    {
        Types.UserInfo storage user = userInfo[msg.sender];
        _withdraw(amount, collateral, to, msg.sender, isInternal);
        uint256 tRate = getTRate();
@>      require(_isAccountSafe(user, tRate), Errors.AFTER_WITHDRAW_ACCOUNT_IS_NOT_SAFE);
    }
```
But in liquidation it accrues rate, which makes tRate higher that it was checked before in `withdraw()`:
```solidity
    function liquidate(
        address liquidated,
        address collateral,
        address liquidator,
        uint256 amount, //@note это сколько ликвидатор хочет получить коллатерала
        bytes memory afterOperationParam,
        uint256 expectPrice
    )
        external
        override
        //@audit немного странно - можно ликвидировать под видом другого юзера
        isValidOperator(msg.sender, liquidator)
        nonFlashLoanReentrant
        returns (Types.LiquidateData memory liquidateData)
    {
@>      accrueRate();
        ...
    }
```

To sum up, if user withdraws collateral near liquidation threshold, it will be liquidated immediately because health check and liquidate use different formulas to calculate tRate - health check underestimates tRate.

## Code Snippet
https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/ed4a8483da11bcc04ced10de899038bcead087b3/smart-contract-EVM/src/JUSDBankStorage.sol#L63-L66
https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/ed4a8483da11bcc04ced10de899038bcead087b3/smart-contract-EVM/src/JUSDBank.sol#L130-L162

## Tool used

Manual Review

## Recommendation
Refactor `getTRate()`:
```diff
    function getTRate() public view returns (uint256) {
        uint256 timeDifference = block.timestamp - uint256(lastUpdateTimestamp);
-       return tRate + (borrowFeeRate * timeDifference) / Types.SECONDS_PER_YEAR;
+       return tRate.decimalMul((timeDifference * borrowFeeRate) / Types.SECONDS_PER_YEAR + 1e18);
    }
```
