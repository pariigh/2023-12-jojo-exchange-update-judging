Sparkly Sepia Raven

high

# A spender(operator) cannot request withdraw on behalf of the client due to a error in values

## Summary
In requestWithdraw function instead of from address i.e the client address, the address of msg.sender which is the operator(spender) is written which makes the request withdraw functionality useless .

## Vulnerability Detail
Following is the request withdraw function
```solidity
 function requestWithdraw(
        Types.State storage state,
        address from,
        uint256 primaryAmount,
        uint256 secondaryAmount
    )
        external
    {
        require(isWithdrawValid(state, msg.sender, from, primaryAmount, secondaryAmount), Errors.WITHDRAW_INVALID);
        state.pendingPrimaryWithdraw[msg.sender] = primaryAmount;
        state.pendingSecondaryWithdraw[msg.sender] = secondaryAmount;
        state.withdrawExecutionTimestamp[msg.sender] = block.timestamp + state.withdrawTimeLock;
        emit RequestWithdraw(msg.sender, primaryAmount, secondaryAmount, state.withdrawExecutionTimestamp[msg.sender]);
    }
```
The require statement checks whether the spender is allowed to make such requests by checking the primary and secondary amounts with the actual permitted amounts which is as follows
```solidity
function isWithdrawValid(
        Types.State storage state,
        address spender,
        address from,
        uint256 primaryAmount,
        uint256 secondaryAmount
    )
        internal
        view
        returns (bool)
    {
        return spender == from
            || (
                state.primaryCreditAllowed[from][spender] >= primaryAmount
                    && state.secondaryCreditAllowed[from][spender] >= secondaryAmount
            );
    }
```
msg.sender in the requestwithdraw  function is the spender which is checked in isWithdrawValid function
So now coming back to the request withdraw function following changes are done 
```solidity
  state.pendingPrimaryWithdraw[msg.sender] = primaryAmount;
        state.pendingSecondaryWithdraw[msg.sender] = secondaryAmount;
        state.withdrawExecutionTimestamp[msg.sender] = block.timestamp + state.withdrawTimeLock;
```
as said before msg.sender is the spender who withdraws on the behalf of the client (from address) but in the above code pending withdrawals of msg.sender are increased instead of the from address due to this the functionality of spending on behalf of other users becomes useless.

This can also be seen when execute withdraw is called 
```solidity
function executeWithdraw(
        Types.State storage state,
        address from,
        address to,
        bool isInternal,
        bytes memory param
    )
        external
    {
        require(state.withdrawExecutionTimestamp[from] <= block.timestamp, Errors.WITHDRAW_PENDING);
        uint256 primaryAmount = state.pendingPrimaryWithdraw[from];
        uint256 secondaryAmount = state.pendingSecondaryWithdraw[from];
        require(isWithdrawValid(state, msg.sender, from, primaryAmount, secondaryAmount), Errors.WITHDRAW_INVALID);
        state.pendingPrimaryWithdraw[from] = 0;
        state.pendingSecondaryWithdraw[from] = 0;
        // No need to change withdrawExecutionTimestamp, because we set pending
        // withdraw amount to 0.
        _withdraw(state, msg.sender, from, to, primaryAmount, secondaryAmount, isInternal, param);
    }
```
from above when a spender calls this then the values of the following will be zero 
 uint256 primaryAmount = state.pendingPrimaryWithdraw[from];
        uint256 secondaryAmount = state.pendingSecondaryWithdraw[from]; because of the fact the code used msg.sender which is the spender in request withdraw and is using from in execute withdraw.
## Impact
Make the functionality of requesting and executing withdraw on behalf of the client useless.
## Code Snippet
https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/main/smart-contract-EVM/src/libraries/Funding.sol#L78
## Tool used

Manual Review

## Recommendation
make the following changes
```solidity
 function requestWithdraw(
        Types.State storage state,
        address from,
        uint256 primaryAmount,
        uint256 secondaryAmount
    )
        external
    {
        require(isWithdrawValid(state, msg.sender, from, primaryAmount, secondaryAmount), Errors.WITHDRAW_INVALID);
        state.pendingPrimaryWithdraw[from] = primaryAmount;
        state.pendingSecondaryWithdraw[from] = secondaryAmount;
        state.withdrawExecutionTimestamp[from] = block.timestamp + state.withdrawTimeLock;
        emit RequestWithdraw(msg.sender, primaryAmount, secondaryAmount, state.withdrawExecutionTimestamp[msg.sender]);
    }
```