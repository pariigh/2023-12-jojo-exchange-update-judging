Fun Currant Elk

medium

# JUSDOperation.sol#initReserve function misses the check of _initialMortgageRate < _liquidationMortgageRate.

## Summary
`JUSDOperation.sol#initReserve` function misses the check of `_initialMortgageRate < _liquidationMortgageRate`.
Therefore, administrator can set `_initialMortgageRate > _liquidationMortgageRate` by mistake.

## Vulnerability Detail
`JUSDOperation.sol#updateRiskParam` function and `updateReserveParam` function are the following.
```solidity
    function updateRiskParam(
        address collateral,
        uint256 _liquidationMortgageRate,
        uint256 _liquidationPriceOff,
        uint256 _insuranceFeeRate
    )
        external
        onlyOwner
    {
        require(
            Types.ONE - _liquidationMortgageRate
                > _liquidationPriceOff + ((Types.ONE - _liquidationPriceOff) * _insuranceFeeRate) / Types.ONE,
            Errors.RESERVE_PARAM_ERROR
        );

165:    require(reserveInfo[collateral].initialMortgageRate < _liquidationMortgageRate, Errors.RESERVE_PARAM_WRONG);
        reserveInfo[collateral].liquidationMortgageRate = _liquidationMortgageRate;
        reserveInfo[collateral].liquidationPriceOff = _liquidationPriceOff;
        reserveInfo[collateral].insuranceFeeRate = _insuranceFeeRate;
        emit UpdateReserveRiskParam(collateral, _liquidationMortgageRate, _liquidationPriceOff, _insuranceFeeRate);
    }

    /// @notice update the reserve basic params
    function updateReserveParam(
        address collateral,
        uint256 _initialMortgageRate,
        uint256 _maxTotalDepositAmount,
        uint256 _maxDepositAmountPerAccount,
        uint256 _maxColBorrowPerAccount
    )
        external
        onlyOwner
    {
183:    require(_initialMortgageRate < reserveInfo[collateral].liquidationMortgageRate, Errors.RESERVE_PARAM_WRONG);
        reserveInfo[collateral].initialMortgageRate = _initialMortgageRate;
        reserveInfo[collateral].maxTotalDepositAmount = _maxTotalDepositAmount;
        reserveInfo[collateral].maxDepositAmountPerAccount = _maxDepositAmountPerAccount;
        reserveInfo[collateral].maxColBorrowPerAccount = _maxColBorrowPerAccount;
        emit UpdateReserveParam(
            collateral,
            _initialMortgageRate,
            _maxTotalDepositAmount,
            _maxDepositAmountPerAccount,
            _maxColBorrowPerAccount
        );
    }
```
`L165` and `L183` check the condition of `initialMortgageRate < liquidationMortgageRate`.
On the other hand, `JUSDOperation.sol#initReserve` function is the following.
```solidity
    function initReserve(
        address _collateral,
        uint256 _initialMortgageRate,
        uint256 _maxTotalDepositAmount,
        uint256 _maxDepositAmountPerAccount,
        uint256 _maxColBorrowPerAccount,
        uint256 _liquidationMortgageRate,
        uint256 _liquidationPriceOff,
        uint256 _insuranceFeeRate,
        address _oracle
    )
        external
        onlyOwner
    {
        require(
            Types.ONE - _liquidationMortgageRate
                > _liquidationPriceOff + (Types.ONE - _liquidationPriceOff).decimalMul(_insuranceFeeRate),
            Errors.RESERVE_PARAM_ERROR
        );
        reserveInfo[_collateral].initialMortgageRate = _initialMortgageRate;
        reserveInfo[_collateral].maxTotalDepositAmount = _maxTotalDepositAmount;
        reserveInfo[_collateral].maxDepositAmountPerAccount = _maxDepositAmountPerAccount;
        reserveInfo[_collateral].maxColBorrowPerAccount = _maxColBorrowPerAccount;
        reserveInfo[_collateral].liquidationMortgageRate = _liquidationMortgageRate;
        reserveInfo[_collateral].liquidationPriceOff = _liquidationPriceOff;
        reserveInfo[_collateral].insuranceFeeRate = _insuranceFeeRate;
        reserveInfo[_collateral].isDepositAllowed = true;
        reserveInfo[_collateral].isBorrowAllowed = true;
        reserveInfo[_collateral].oracle = _oracle;
        _addReserve(_collateral);
    }
```
As can be seen, the above function doesn't check `initialMortgageRate < liquidationMortgageRate`.
Thus administrator can call `initReserve` function with `_initialMortgageRate > _liquidationMortgageRate` by mistake.
In this context, user can be liquidated as soon as he borrows JUSD tokens.

Example:
1. Administrator calls `initReserve` function with `_initialMortgageRate = 7e17 > _liquidationMortgageRate = 8e17` and `_collateral = USDC`.
2. Assume that the price of USDC is exactly 1$. User can deposit 1000 of USDC and borrow `1000 * _initialMortgageRate = 700` of JUSD.
3. Since `700 < 1000 * _liquidationMortgageRate = 800`, user can be liquidated immediately.

## Impact
As soon as user borrow JUSD tokens, he can be liquidated by administrator's mistake.
When user is liquidated, he can be damaged due to `_liquidationPriceOff` and `_insuranceFeeRate`.

## Code Snippet
https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/main/smart-contract-EVM/src/JUSDOperation.sol#L52

## Tool used
Manual Review

## Recommendation
Modify `JUSDOperation.sol#initReserve` function as follows.
```solidity
    function initReserve(
        address _collateral,
        uint256 _initialMortgageRate,
        uint256 _maxTotalDepositAmount,
        uint256 _maxDepositAmountPerAccount,
        uint256 _maxColBorrowPerAccount,
        uint256 _liquidationMortgageRate,
        uint256 _liquidationPriceOff,
        uint256 _insuranceFeeRate,
        address _oracle
    )
        external
        onlyOwner
    {
        require(
            Types.ONE - _liquidationMortgageRate
                > _liquidationPriceOff + (Types.ONE - _liquidationPriceOff).decimalMul(_insuranceFeeRate),
            Errors.RESERVE_PARAM_ERROR
        );
++      require(_initialMortgageRate < _liquidationMortgageRate, Errors.RESERVE_PARAM_WRONG);
        reserveInfo[_collateral].initialMortgageRate = _initialMortgageRate;
        reserveInfo[_collateral].maxTotalDepositAmount = _maxTotalDepositAmount;
        reserveInfo[_collateral].maxDepositAmountPerAccount = _maxDepositAmountPerAccount;
        reserveInfo[_collateral].maxColBorrowPerAccount = _maxColBorrowPerAccount;
        reserveInfo[_collateral].liquidationMortgageRate = _liquidationMortgageRate;
        reserveInfo[_collateral].liquidationPriceOff = _liquidationPriceOff;
        reserveInfo[_collateral].insuranceFeeRate = _insuranceFeeRate;
        reserveInfo[_collateral].isDepositAllowed = true;
        reserveInfo[_collateral].isBorrowAllowed = true;
        reserveInfo[_collateral].oracle = _oracle;
        _addReserve(_collateral);
    }
```
