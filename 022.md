Prehistoric Sand Turtle

medium

# safeApprove() function may reverts for changing existing approvals

## Summary

SafeERC20.safeApprove reverts if there is an attempt to change a non-zero approval to another non-zero approval. In the `FundingRateArbitrage._swap()` function, such an attempt may occur, resulting in a revert.

## Vulnerability Detail

The `safeApprove()` function has explicit warning:

```solidity=46
    // safeApprove should only be called when setting an initial allowance,
    // or when resetting it to zero. To increase and decrease it, use
    // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
```

At line 207, the `FundingRateArbitrage` contract uses the`safeApprove()` function to change the approval amount.

```solidity=194
function _swap(bytes memory param, bool isBuyingEth) private returns (uint256 receivedAmount) {
    address fromToken;
    address toToken;
    if (isBuyingEth) {
        fromToken = usdc;
        toToken = collateral;
    } else {
        fromToken = collateral;
        toToken = usdc;
    }
    uint256 toTokenReserve = IERC20(toToken).balanceOf(address(this));
    (address approveTarget, address swapTarget, uint256 payAmount, bytes memory callData) =
        abi.decode(param, (address, address, uint256, bytes));
    IERC20(fromToken).safeApprove(approveTarget, payAmount);
    (bool isSuccess,) = swapTarget.call(callData);
    if (!isSuccess) {
        assembly {
            let ptr := mload(0x40)
            let size := returndatasize()
            returndatacopy(ptr, 0, size)
            revert(ptr, size)
        }
    }
    receivedAmount = IERC20(toToken).balanceOf(address(this)) - toTokenReserve;
    emit Swap(fromToken, toToken, payAmount, receivedAmount);
}
```

https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/main/smart-contract-EVM/src/FundingRateArbitrage.sol#L194-L219

With the external contract intregated, there might be remaining allowance to the target contract. As a result, the owner might unable to call `swapBuyEth()` or `swapSellEth()` functions.

## Impact

The contract owner might be unable to swap USDC for collateral tokens and deposit them to the collateral system or withdraw collateral tokens to the pool and swap them back to USDC.

## Code Snippet

https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/main/smart-contract-EVM/src/FundingRateArbitrage.sol#L207

## Tool used

Manual Review

## Recommendation

Resetting the allowance to zero before calling `safeApprove()` function.