Modern Mint Haddock

high

# Instant Liquidation Risk Due to maxColBorrowPerAccount Reduction in Smart Contract

## Summary
 A reduction in the `maxColBorrowPerAccount` parameter can lead to the immediate liquidation of user accounts. This occurs because the liquidation mechanism relies on the `maxColBorrowPerAccount` value to calculate the maximum amount that can be borrowed against collateral, potentially understating the actual safe borrowing limit.

## Vulnerability Detail
`borrow()`, `withdraw()`, and `liquidate()` functions interact with user collateral and borrowing limits. The core issue arises from the use of `_getMintAmount()` in conjunction with the `maxColBorrowPerAccount` parameter:
```solidity
function _getMintAmount(Types.ReserveInfo memory reserve, uint256 amount, uint256 rate) internal view returns (uint256) {
    uint256 depositAmount = IPriceSource(reserve.oracle).getAssetPrice().decimalMul(amount).decimalMul(rate);
    if (depositAmount >= reserve.maxColBorrowPerAccount) {
        depositAmount = reserve.maxColBorrowPerAccount;
    }
    return depositAmount;
}
```

This function calculates the maximum `JUSD` that can be minted against a given amount of collateral at a specified rate. However, it caps the mintable amount at `maxColBorrowPerAccount`, potentially reducing the actual amount that can be safely borrowed which is good for `isAccountSafe()` function used when a user request to borrow or withdraw but for `isStartLiquidation()` , a better would be use directly ` IPriceSource(reserve.oracle).getAssetPrice().decimalMul(amount).decimalMul(rate)` to account for the real amount borrowed by a user : 

```solidity
function _isStartLiquidation(Types.UserInfo storage liquidatedTraderInfo, uint256 tRate) internal view returns (bool) {
    uint256 JUSDBorrow = liquidatedTraderInfo.t0BorrowBalance.decimalMul(tRate);
    uint256 liquidationMaxMintAmount;
    address[] memory collaterals = liquidatedTraderInfo.collateralList;
    for (uint256 i; i < collaterals.length; i++) {
        address collateral = collaterals[i];
        Types.ReserveInfo memory reserve = reserveInfo[collateral];
        liquidationMaxMintAmount += _getMintAmount(reserve, liquidatedTraderInfo.depositBalance[collateral], reserve.liquidationMortgageRate);
    }
    return liquidationMaxMintAmount < JUSDBorrow;
}

```
This function determines if an account should start liquidation. It compares the total amount that can be safely minted against the user's collaterals (`liquidationMaxMintAmount`) with the user's borrowed amount (`JUSDBorrow`). If the mintable amount is less than the borrowed amount, the account is marked for liquidation.

## Impact
When `maxColBorrowPerAccount` is decreased (e.g., via `updateReserveParam()`), the `liquidationMaxMintAmount` calculated in `_isStartLiquidation` may suddenly drop, even though the user's actual collateral value hasn't changed.
This could lead to users being instantly marked for liquidation, even though their collateral position remains the same.

## Code Snippet
https://github.com/sherlock-audit/2023-12-jojo-exchange-update/blob/main/smart-contract-EVM/src/JUSDView.sol#L145-L149

## Tool used

Manual Review

## Recommendation

Modify `_isStartLiquidation()` to calculate `liquidationMaxMintAmount` without relying on the `maxColBorrowPerAccount` cap. This would more accurately reflect the real value of collateral at risk of liquidation.
```solidity
function _isStartLiquidation(Types.UserInfo storage liquidatedTraderInfo, uint256 tRate) internal view returns (bool) {
    uint256 JUSDBorrow = liquidatedTraderInfo.t0BorrowBalance.decimalMul(tRate);
    uint256 liquidationMaxMintAmount;
    address[] memory collaterals = liquidatedTraderInfo.collateralList;
    for (uint256 i; i < collaterals.length; i++) {
        address collateral = collaterals[i];
        Types.ReserveInfo memory reserve = reserveInfo[collateral];
-       liquidationMaxMintAmount += _getMintAmount(reserve, liquidatedTraderInfo.depositBalance[collateral], reserve.liquidationMortgageRate);
+       liquidationMaxMintAmount += IPriceSource(reserve.oracle).getAssetPrice().decimalMul(liquidatedTraderInfo.depositBalance[collateral]).decimalMul(reserve.liquidationMortgageRate);
    }
    return liquidationMaxMintAmount < JUSDBorrow;
}
```